give me all b-sides of the ramones
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX res: <http://dbpedia.org/resource/>
SELECT DISTINCT ?string 
WHERE {
        ?x dbo:musicalArtist res:Ramones .
        ?x dbo:bSide ?string . 
}

give me the birthdays of all actors of the television show charmed
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX res: <http://dbpedia.org/resource/>
SELECT DISTINCT ?date 
WHERE { 
        res:Charmed dbo:starring ?actor .
        ?actor dbo:birthDate ?date .
}

which software was developed_by organizations founded_in california
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbp: <http://dbpedia.org/property/>
PREFIX res: <http://dbpedia.org/resource/>
SELECT DISTINCT ?uri
WHERE {         
        ?uri rdf:type dbo:Software .
        ?company rdf:type dbo:Company .
        ?uri dbo:developer ?company . 
        ?company dbo:foundationPlace res:California .
}

which countries have more than two official languages
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT DISTINCT ?uri 
WHERE { 
        ?uri rdf:type dbo:Country  .
        ?uri dbo:officialLanguage ?language .
} 
GROUP BY ?uri
HAVING (COUNT(?language) > 2)

what is the founding year of the brewery that produces pilsner urquell
PREFIX dbp: <http://dbpedia.org/property/>
PREFIX res: <http://dbpedia.org/resource/>
SELECT DISTINCT ?num 
WHERE {
        res:Pilsner_Urquell dbp:brewery ?uri .
        ?uri dbp:foundation ?num .
}

give me all people that were born in vienna and died in berlin
PREFIX dbo: <http://dbpedia.org/ontology/> 
PREFIX res: <http://dbpedia.org/resource/> 
SELECT DISTINCT ?uri 
WHERE { 
        ?uri dbo:birthPlace res:Vienna . 
        ?uri dbo:deathPlace res:Berlin . 
}


what is the total amount of men and women serving in the fdny
PREFIX dbp: <http://dbpedia.org/property/> 
PREFIX res: <http://dbpedia.org/resource/> 
SELECT DISTINCT ?num WHERE { res:New_York_City_Fire_Department dbp:strength ?num . 
}


who composed the music for harold and maude
PREFIX res: <http://dbpedia.org/resource/>
PREFIX dbo: <http://dbpedia.org/ontology/>
SELECT DISTINCT ?uri 
WHERE {
       res:Harold_and_Maude dbo:musicComposer ?uri .
}

who wrote the book the pillars of the earth
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX res: <http://dbpedia.org/resource/>
SELECT DISTINCT ?uri 
WHERE {
        res:The_Pillars_of_the_Earth dbo:author ?uri .
}

which companies work in the aerospace industry as well as in medicine
PREFIX dbo:  <http://dbpedia.org/ontology/>
PREFIX res:  <http://dbpedia.org/resource/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT DISTINCT ?uri 
WHERE { 
        ?uri rdf:type dbo:Company  .
        ?uri dbo:industry res:Aerospace . 
        ?uri dbo:industry res:Medicine .
}

which countries have more than ten caves
PREFIX dbo: <http://dbpedia.org/ontology/> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
SELECT DISTINCT ?uri 
WHERE { 
        ?uri rdf:type dbo:Country . 
	?cave rdf:type dbo:Cave .
	?cave dbo:location ?uri .  
} 
GROUP BY ?uri HAVING(COUNT(?cave) > 10)

which country does the creator of miffy come from
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX res: <http://dbpedia.org/resource/>
SELECT DISTINCT ?uri 
WHERE {
        res:Miffy dbo:creator ?x .
        ?x dbo:nationality ?uri .
}

was margaret thatcher a chemist
PREFIX res: <http://dbpedia.org/resource/> 
PREFIX dbo: <http://dbpedia.org/ontology/> 
ASK 
WHERE { 
        res:Margaret_Thatcher dbo:profession res:Chemist . 
}

was marc chagall a jew
PREFIX dbp: <http://dbpedia.org/property/>
PREFIX res: <http://dbpedia.org/resource/>
ASK 
WHERE {
       res:Marc_Chagall dbp:ethnicity 'Jewish'@en .
}

how many aldi stores are there
PREFIX res: <http://dbpedia.org/resource/>
PREFIX dbo: <http://dbpedia.org/ontology/>
SELECT DISTINCT ?number
WHERE {
        res:Aldi dbo:numberOfLocations ?number .
}


for which label did elvis record his first album
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX res: <http://dbpedia.org/resource/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT DISTINCT ?uri 
WHERE {
        ?x rdf:type dbo:Album .
        ?x dbo:artist res:Elvis_Presley .
        ?x dbo:releaseDate ?y .
        ?x dbo:recordLabel ?uri .
}
ORDER BY ASC(?y) 
OFFSET 0 LIMIT 1

in which military conflicts did lawrence of arabia participate
PREFIX dbo: <http://dbpedia.org/ontology/> 
PREFIX res: <http://dbpedia.org/resource/> 
SELECT DISTINCT ?uri 
WHERE { 
        res:T._E._Lawrence dbo:battle ?uri . 
}

who was the first president of the united states
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT DISTINCT ?uri
WHERE {
         ?uri rdf:type dbo:Person .
         ?uri dbo:office '1st President of the United States'@en .
}

how many children did benjamin franklin have
PREFIX res: <http://dbpedia.org/resource/> 
PREFIX dbo: <http://dbpedia.org/ontology/> 
SELECT (COUNT(DISTINCT ?uri) as ?ss) 
WHERE { 
        res:Benjamin_Franklin dbo:child ?uri . 
}

give me all actors starring in movies directed by william shatner
PREFIX dbo: <http://dbpedia.org/ontology/> 
PREFIX dbp: <http://dbpedia.org/property/> 
PREFIX res: <http://dbpedia.org/resource/> 
SELECT DISTINCT ?uri 
WHERE { 
        ?x dbo:director res:William_Shatner . 
        ?x dbo:starring ?uri . 
}

who produced films starring natalie portman
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX res: <http://dbpedia.org/resource/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT DISTINCT ?uri 
WHERE {
        ?film rdf:type dbo:Film .
        ?film dbo:starring res:Natalie_Portman .
        ?film dbo:producer ?uri .
}

in which films did julia roberts as well as richard gere play
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX res: <http://dbpedia.org/resource/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT DISTINCT ?uri 
WHERE {
        ?uri rdf:type dbo:Film .
        ?uri dbo:starring res:Julia_Roberts .
        ?uri dbo:starring res:Richard_Gere.
}

was natalie portman born in the united states
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX res: <http://dbpedia.org/resource/>
ASK 
WHERE { 
        res:Natalie_Portman dbo:birthPlace ?city .
        ?city dbo:country res:United_States .
}


give me all launch pads operated by nasa
PREFIX dbo: <http://dbpedia.org/ontology/> 
PREFIX res: <http://dbpedia.org/resource/> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
SELECT DISTINCT ?uri 
WHERE { 
        ?uri rdf:type dbo:LaunchPad .  
        ?uri dbo:operator res:NASA . 
}


who wrote the lyrics for the polish national anthem
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbp: <http://dbpedia.org/property/>
PREFIX res: <http://dbpedia.org/resource/>
SELECT DISTINCT ?uri 
WHERE {
        res:Poland dbo:anthem ?x .
        ?x dbp:author ?uri .
}


is there a video game called battle chess
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
ASK 
WHERE {
        ?uri rdf:type dbo:VideoGame .
        ?uri rdfs:label 'Battle Chess'@en .
}


in which uk city are the headquarters of the mi6
PREFIX res: <http://dbpedia.org/resource/> 
PREFIX dbo: <http://dbpedia.org/ontology/> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT DISTINCT ?uri 
WHERE { 
        ?uri rdf:type dbo:City . 
        res:Secret_Intelligence_Service dbo:headquarter ?uri . 
}


in which films directed by garry marshall was julia roberts starring
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX res: <http://dbpedia.org/resource/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT DISTINCT ?uri
WHERE {
        ?uri rdf:type dbo:Film .
        ?uri dbo:starring res:Julia_Roberts .
        ?uri dbo:director res:Garry_Marshall .
}

through which countries does the yenisei river flow
PREFIX res: <http://dbpedia.org/resource/>
PREFIX dbo: <http://dbpedia.org/ontology/>
SELECT DISTINCT ?uri 
WHERE {
        res:Yenisei_River dbo:country ?uri .
}

to which countries does the himalayan mountain system extend
PREFIX dbp: <http://dbpedia.org/property/> 
PREFIX res: <http://dbpedia.org/resource/> 
SELECT DISTINCT ?uri 
WHERE { 
        res:Himalayas dbp:country ?uri . 
}

which countries are connected by the rhine
PREFIX dbo: <http://dbpedia.org/ontology/> 
PREFIX dbp: <http://dbpedia.org/property/> 
PREFIX res: <http://dbpedia.org/resource/> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
SELECT DISTINCT ?uri 
WHERE { 
        res:Rhine dbo:country ?string .  
        ?uri rdf:type dbo:Country .  
        ?uri rdfs:label ?string . 
}

give me all cosmonauts
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX dbo: <http://dbpedia.org/ontology/> 
PREFIX res: <http://dbpedia.org/resource/> 
SELECT DISTINCT ?uri 
WHERE { 
        ?uri rdf:type dbo:Astronaut . 
      ?uri dbo:nationality res:Russia . 
}



is proinsulin a protein
PREFIX dbo: <http://dbpedia.org/ontology/> 
PREFIX res: <http://dbpedia.org/resource/> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
ASK 
WHERE { 
        res:Proinsulin rdf:type dbo:Protein . 
}


who is the daughter of ingrid bergman married to
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX res: <http://dbpedia.org/resource/>
SELECT DISTINCT ?uri
WHERE {
        res:Ingrid_Bergman dbo:child ?child .
        ?child dbo:spouse ?uri .
}


how many people live in the capital of australia
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX res: <http://dbpedia.org/resource/>
SELECT DISTINCT ?num 
WHERE {
        res:Australia dbo:capital ?x .
        ?x dbo:populationTotal ?num .
}

which music albums contain the song last christmas
PREFIX dbo:  <http://dbpedia.org/ontology/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?uri 
WHERE {
        ?single dbo:album ?uri .
        ?single rdfs:label 'Last Christmas'@en .
}
